<programa> ::= id ":" <variaveis> <lista_de_comandos>;
<variaveis> ::= î | <tipo> <lista_id_var> "." <variaveis>;
<lista_de_comandos>::= <operacao> <lista_de_comandos_A> | <saida_de_dados> <lista_de_comandos_A> | <selecao> <lista_de_comandos_A> | <repeticao> <lista_de_comandos_A>;
<lista_de_comandos_A>::= î | <lista_de_comandos>;
<tipo> ::= <simples> | <array>;
<array> ::= array "[" <simples> "," c_int "]";
<lista_id_var> ::= id | id "," <lista_id_var>;
<simples> ::= c_int | c_float | c_str;
<operacao> ::= <atribuicao> | <entrada_de_dados>;
<atribuicao> ::= id "=" <expressao> | id "+=" <expressao> | id "-=" <expressao>;
<entrada_de_dados> ::= <entrada_de_dados_com_mensagem> | <entrada_de_dados_sem_mensagem>;
<entrada_de_dados_com_mensagem> ::= id "=" input "(" c_str ")" | id "=" <tipo> "(" input "(" c_str ")" ")";
<entrada_de_dados_sem_mensagem> ::= id "=" input "(" ")" | id "=" <tipo> input "(" ")";
<saida_de_dados> ::= print "(" <lista_de_expressoes> ")";
<lista_de_expressoes> ::= <expressao> | <expressao> "," <lista_de_expressoes>;
<selecao> ::= <selecao_simples> | <selecao_senao> | <selecao_composta>;
<selecao_simples> ::= if <expressao> ":" <lista_de_comandos> end;
<selecao_senao> ::= if <expressao> ":" <lista_de_comandos> else ":" <lista_de_comandos> end;
<selecao_composta> ::= if <expressao> ":" <lista_de_comandos> <selecao_alternativa> end | if <expressao> ":" <lista_de_comandos> <selecao_alternativa> else ":" <lista_de_comandos> end;
<selecao_alternativa> ::= elif <expressao> ":" <lista_de_comandos> | elif <expressao> ":" <lista_de_comandos> <selecao_alternativa>;
<repeticao> ::= <repeticao_while> | <repeticao_for_range> | <repeticao_for_each>;
<repeticao_while> ::= while <expressao> ":" <lista_de_comandos> end;
<repeticao_for_range> ::= for id in range "(" <expressao> ")" ":" <lista_de_comandos> end;
<repeticao_for_each> ::=  for id in id ":" <lista_de_comandos> end;
<expressao> ::= <elemento> <expressao_>;
<expressao_> ::= and <elemento> <expressao_> | or <elemento> <expressao_> | î;
<elemento> ::= <relacional> | true | false | not <elemento>;
<relacional> ::= <aritmetica> <operador_relacional> <aritmetica> | <aritmetica>;
<operador_relacional> ::= "==" | "!=" | "<" | "<=" | ">" | ">=";
<aritmetica> ::= <termo> <aritmetica_>;
<aritmetica_> ::= "+" <termo>  <aritmetica_> | "-" <termo> <aritmetica_> | î;
<termo> ::= <fator> <termo_>;
<termo_> ::= "*" <fator> <termo_> | "/" <fator> <termo_> | "//" <fator> <termo_> |  "%" <fator> <termo_> | î;
<fator>	::= id | c_int | c_float | c_str | "(" <expressao> ")" | "+" <fator> | "-" <fator>;